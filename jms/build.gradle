plugins {
	id 'java-library'
	id "io.freefair.lombok" version "$lombokVersion"
}

apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'maven-publish'

delombok {
	enabled = false
}

jar {
	enabled = true
}

bootJar {
	enabled = false
}

dependencies {
	annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

	api project(":api")
	api project(":app")

	api "org.springframework.boot:spring-boot-starter-activemq"

	api "org.apache.activemq:activemq-broker"
	api "org.apache.activemq:activemq-client"
	api "org.apache.activemq:activemq-kahadb-store"
	api "org.apache.activemq:activemq-pool"
	api "org.apache.activemq:activemq-spring"

	compileOnly "org.springframework.cloud:spring-cloud-starter-netflix-eureka-client:$springCloudVersion"

	testImplementation "org.springframework.boot:spring-boot-starter-test"
	testImplementation "org.springframework.boot:spring-boot-starter-activemq"

	testImplementation "org.junit.jupiter:junit-jupiter:5.8.1"

	testImplementation "org.apache.activemq:activemq-client"
	testImplementation "org.apache.activemq:activemq-broker"
	testImplementation "org.apache.activemq:activemq-pool"
	testImplementation "org.apache.activemq:activemq-spring"
}

test {
	useJUnitPlatform()
}

javadoc {
	if (JavaVersion.current().isJava9Compatible()) {
		options.addBooleanOption('html5', true)
		options.encoding('UTF-8')
	}
}

tasks.register('sourceJar', Jar) {
	archiveClassifier = 'sources'
	from sourceSets.main.allJava
}

tasks.register('javadocJar', Jar) {
	archiveClassifier = 'javadoc'
	from javadoc
}

publishing {
	publications {
		maven(MavenPublication) {
			groupId = project.group
			artifactId = project.name
			version = project.version

			from components.java
			artifact sourceJar
			artifact javadocJar
		}
	}

	repositories {
		maven {
			url 'https://nexus.proitr.ru/repository/maven-proit/'
			credentials {
				username nexusLogin
				password nexusPassword
			}
		}
	}
}