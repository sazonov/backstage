plugins {
	id 'java-library'
	id "io.freefair.lombok" version "$lombokVersion"
}

apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'maven-publish'
apply plugin: 'proit-jpa-weaving'
apply plugin: 'proit-jpa-modelgen'

delombok {
	enabled = false
}

jar {
	enabled = true
}

bootJar {
	enabled = false
}

dependencies {
	annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

	api project(":app")
	compileOnly "org.springframework.boot:spring-boot-starter-data-mongodb"
	api ("org.springframework.data:spring-data-jpa") {
		exclude(group: "org.hibernate")
	}

	api "org.flywaydb:flyway-core"

	compileOnly "net.postgis:postgis-jdbc:$postgisVersion"
	compileOnly "org.geolatte:geolatte-geom:$geolatteVersion"

	api "org.postgresql:postgresql"
	api "com.zaxxer:HikariCP"

	api "org.eclipse.persistence:eclipselink:$eclipseLinkVersion"

	testImplementation project(":app")

	testImplementation "com.zaxxer:HikariCP"

	testImplementation "org.springframework.boot:spring-boot-starter-test"
	testImplementation "com.zaxxer:HikariCP"

	testImplementation "org.junit.jupiter:junit-jupiter:5.8.1"

	testImplementation "org.testcontainers:testcontainers:$testContainersVersion"
	testImplementation "org.testcontainers:junit-jupiter:$testContainersVersion"
	testImplementation "org.testcontainers:postgresql:$testContainersVersion"
}

test {
	useJUnitPlatform()
}

javadoc {
	if (JavaVersion.current().isJava9Compatible()) {
		options.addBooleanOption('html5', true)
		options.encoding('UTF-8')
	}
}

tasks.register('sourceJar', Jar) {
	archiveClassifier = 'sources'
	from sourceSets.main.allJava
}

tasks.register('javadocJar', Jar) {
	archiveClassifier = 'javadoc'
	from javadoc
}

publishing {
	publications {
		maven(MavenPublication) {
			groupId = project.group
			artifactId = project.name
			version = project.version

			from components.java
			artifact sourceJar
			artifact javadocJar
		}
	}

	repositories {
		maven {
			url 'https://nexus.proitr.ru/repository/maven-proit/'
			credentials {
				username nexusLogin
				password nexusPassword
			}
		}
	}
}