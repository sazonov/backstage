plugins {
	id 'java-library'
	id "io.freefair.lombok" version "$lombokVersion"
}

apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'maven-publish'
apply plugin: 'proit-jpa-weaving'
apply plugin: 'proit-jpa-modelgen'

delombok {
	enabled = false
}

jar {
	enabled = true
}

bootJar {
	enabled = false
}

dependencies {
	annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

	api project(":api")
	api project(":jms")

	api "org.springframework.boot:spring-boot-starter-cache"

	api "jakarta.persistence:jakarta.persistence-api"
	api "jakarta.activation:jakarta.activation-api:2.1.0"

	// TODO: разделить на два модуля
	compileOnly "org.ehcache:ehcache"
	compileOnly "com.hazelcast:hazelcast-spring"

	testImplementation "org.springframework.boot:spring-boot-starter-test"

	testImplementation "org.ehcache:ehcache"

	testImplementation "org.testcontainers:testcontainers:$testContainersVersion"
	testImplementation "org.testcontainers:junit-jupiter:$testContainersVersion"
	testImplementation "org.testcontainers:postgresql:$testContainersVersion"
}

test {
	useJUnitPlatform()
}

javadoc {
	if (JavaVersion.current().isJava9Compatible()) {
		options.addBooleanOption('html5', true)
		options.encoding('UTF-8')
	}
}

tasks.register('sourceJar', Jar) {
	archiveClassifier = 'sources'
	from sourceSets.main.allJava
}

tasks.register('javadocJar', Jar) {
	archiveClassifier = 'javadoc'
	from javadoc
}

publishing {
	publications {
		maven(MavenPublication) {
			groupId = project.group
			artifactId = project.name
			version = project.version

			from components.java
			artifact sourceJar
			artifact javadocJar
		}
	}

	repositories {
		maven {
			url 'https://nexus.proitr.ru/repository/maven-proit/'
			credentials {
				username nexusLogin
				password nexusPassword
			}
		}
	}
}